カメ画像URL="https://f.easyuploader.app/eu-prd/upload/20200525150519_71696934704838673179.png"

色最大 = 255

●小さい方(値A, 値B)
    もし、値Bが値Aを超えるならば
        値Aで戻る
    ここまで

    値Bで戻る
ここまで

●描画(k, n, 大きさ, 始点横, 始点縦, 初期赤, 初期緑, 初期青, 緑乱数範囲, 青乱数範囲)
    [始点横, 始点縦]にカメ起点移動
    i = 0
    t = 0
    滑らかさ = 300

    滑らかさにkを掛けたそれを回す

        赤 = 初期赤
        緑 = 初期緑に緑乱数範囲の乱数を足す
        青 = 初期青に青乱数範囲の乱数を足す

        赤 = 小さい方(色最大, 赤)
        緑 = 小さい方(色最大, 緑)
        青 = 小さい方(色最大, 青)

        線色 = RGB(赤, 緑, 青)
        線色にカメペン色設定

        i = iに1を足す
        t = PIに2を掛けて滑らかさで割って、それにiを掛る
        角度 = nにtを掛けてkで割ったそれのSIN
        移動横 = tのCOSに角度を掛けて、それに大きさを掛る
        移動縦 = tのSINに角度を掛けて、それに大きさを掛る

        [始点横に移動横を足す, 始点縦に移動縦を足す]にカメ移動
    ここまで
ここまで

●あや作成(k, n, 大きさ, 始点横, 始点縦, 初期赤, 初期緑, 初期青, 緑乱数範囲, 青乱数範囲)
    カメ作成
    1にカメ速度設定

    大きさ2 = 大きさを5で割る

    描画(k, n, 大きさ, 始点横, 始点縦, 初期赤, 初期緑, 初期青, 緑乱数範囲, 青乱数範囲)
    描画(k, n, 大きさ2, 始点横, 始点縦, 色最大, 色最大, 0, 0, 0)

    OFFにカメペン設定
    [1000, 0]にカメ移動
    カメ非表示
ここまで

●あや一括作成(データ)
    データを反復
        あや作成(それ[0], それ[1], それ[2], それ[3], それ[4], それ[5], それ[6], それ[7], それ[8], それ[9])
    ここまで
ここまで

●カード(横, 縦, 大きさ, アスキーコード)
    言葉 = ""
    アスキーコードを反復
        言葉 = 言葉&CHR(それ)
    ここまで
    言葉をコンソール表示

   「{大きさ}px Aria」に描画フォント設定
    [横, 縦]へ言葉を文字描画。
ここまで

あやデータ=[
    [1, 4, 110, 160, 160,  25, 80, 190, 100,  60],
    [1, 4, 110, 400, 130, 225, 60, 120,  80, 180],
    [1, 4, 100, 630, 200, 225, 30,  30, 100, 130],
    [1, 4, 100, 530, 450, 225, 60,  70, 120, 170],
    [1, 4, 100, 120, 400, 225, 50,  40, 110, 120],
    [1, 4,  90, 320, 460,  25, 80, 200,  50,  50],
    [1, 4,  80, 690, 420,  25, 80, 180, 150,  50]
];

コード = [72, 65, 80, 80, 89, 32, 66, 73, 82, 84, 72, 68, 65, 89, 32, 33, 33]
コード2 = [45, 32, 78, 105, 97, 32, 45]

逐次実行
    先に、あや一括作成(あやデータ)。
    次に、7秒待機。
    次に、カード(250, 280, 30, コード)。
    次に、カード(310, 330, 50, コード2)。
ここまで
